*          Coil+ Coil- bCOM bNO bNC tCOM tNO tNC
.subckt DPDT  100    200    3    4   5   6    7   8  
B1 1 2 V=abs(V(100)-V(200))
S1 3 5 10 2 SW_NC
S2 3 4 10 2 SW_NO
S3 6 8 10 2 SW_NC
S4 6 7 10 2 SW_NO
R1 3 0 1G
R2 4 0 1G
R3 5 0 1G
R4 6 0 1G
R5 7 0 1G
R6 8 0 1G
R10 10 0 1G
R11 2 0 1G
*R8 C1 simulate relay operate/release delay
R8 10 1 1meg
R9 100 200 {Rcoil}
C1 10 2 15nf
.model SW_NO SW(Ron=1m Roff=1G Vt={abs(Vcoil*0.5)} Vh={abs(Vcoil*0.25)})
.model SW_NC SW(Ron=1G Roff=1m Vt={abs(Vcoil*0.5)} Vh={abs(Vcoil*0.25)})
.ends DPDT